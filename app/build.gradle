apply plugin: "com.android.application"

android {
    compileSdk 35
    namespace "biz.binarysolutions.imageconverter"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    defaultConfig {
        applicationId "biz.binarysolutions.imageconverter"
        minSdk 21
        targetSdk 35
        versionCode 9
        versionName "2.0"

        setProperty("archivesBaseName", "$applicationId-$versionName")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    flavorDimensions = ["version"]
    productFlavors {
        google {
            dimension "version"
            applicationIdSuffix ".ggl"
            manifestPlaceholders["launchActivityName"] = "biz.binarysolutions.imageconverter.MainActivityGoogle"
        }
        free {
            dimension "version"
            manifestPlaceholders["launchActivityName"] = "biz.binarysolutions.imageconverter.MainActivity"

            dependenciesInfo {
                includeInApk = false
                includeInBundle = false
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "androidx.preference:preference:1.2.1"
    // newer versions require compile SDK update
    implementation "androidx.appcompat:appcompat:1.3.0"
    implementation "com.android.support:support-annotations:28.0.0"
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.3.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.7.0"

    implementation "io.github.beyka:Android-TiffBitmapFactory:0.9.9.1"

    // version 24.0.0+ requires min SDK 23
    googleImplementation "com.google.android.gms:play-services-ads:23.6.0"
    googleImplementation "com.android.billingclient:billing:8.0.0"
}

configurations.implementation {
    exclude group: "org.jetbrains.kotlin", module: "kotlin-stdlib-jdk8"
}